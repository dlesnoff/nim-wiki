<!DOCTYPE html>
<html lang="en-us">
<head>
  <title>nimwiki/Templates.nim</title>
  <link rel="icon" href="data:image/svg+xml,<svg xmlns=%22http://www.w3.org/2000/svg%22 viewBox=%220 0 100 100%22><text y=%22.9em%22 font-size=%2280%22>üê≥</text></svg>">
  <meta content="text/html; charset=utf-8" http-equiv="content-type">
  <meta content="width=device-width, initial-scale=1" name="viewport">
  <link rel='stylesheet' href='https://unpkg.com/normalize.css'>
  <link rel="stylesheet" href="https://cdn.jsdelivr.net/gh/kognise/water.css@latest/dist/light.min.css">
  <link rel='stylesheet' href='https://cdn.jsdelivr.net/gh/pietroppeter/nimib/assets/atom-one-light.css'>
  <style>
.nb-box {
  display: flex;
  align-items: center;
  justify-content: space-between;
}
.nb-small {
  font-size: 0.8rem;
}
button.nb-small {
  float: right;
  padding: 2px;
  padding-right: 5px;
  padding-left: 5px;
}
section#source {
  display:none
}

.nb-output {
  line-height: 1.15;
}
</style>
  
</head>
<body>
<header>
<div class="nb-box">
  <span><a href="..">üè°</a></span>
  <span><code>nimwiki/Templates.nim</code></span>
  <span><a href="https://github.com/dlesnoff/nim-wiki"><svg aria-hidden="true" width="1.2em" height="1.2em" style="vertical-align: middle;" preserveAspectRatio="xMidYMid meet" viewBox="0 0 16 16"><path fill-rule="evenodd" d="M8 0C3.58 0 0 3.58 0 8c0 3.54 2.29 6.53 5.47 7.59c.4.07.55-.17.55-.38c0-.19-.01-.82-.01-1.49c-2.01.37-2.53-.49-2.69-.94c-.09-.23-.48-.94-.82-1.13c-.28-.15-.68-.52-.01-.53c.63-.01 1.08.58 1.23.82c.72 1.21 1.87.87 2.33.66c.07-.52.28-.87.51-1.07c-1.78-.2-3.64-.89-3.64-3.95c0-.87.31-1.59.82-2.15c-.08-.2-.36-1.02.08-2.12c0 0 .67-.21 2.2.82c.64-.18 1.32-.27 2-.27c.68 0 1.36.09 2 .27c1.53-1.04 2.2-.82 2.2-.82c.44 1.1.16 1.92.08 2.12c.51.56.82 1.27.82 2.15c0 3.07-1.87 3.75-3.65 3.95c.29.25.54.73.54 1.48c0 1.07-.01 1.93-.01 2.2c0 .21.15.46.55.38A8.013 8.013 0 0 0 16 8c0-4.42-3.58-8-8-8z" fill="#000"></path></svg></a></span>
</div>
<hr>
</header><main>
<h2>Templates</h2>
<p>This page is a draft for brainstorming about Nim templates, before writing a <a href="https://github.com/nim-lang/RFCs">RFC</a>. The goal is to complete the <a href="https://nim-lang.org/docs/manual.html#templates">specification of templates</a> and eventually complete the implementation in the compiler.</p>
<h4>Compiler sources</h4>
<p>Some sources related to templates...</p>
<ul>
<li>Proc and template calls: <a href="https://github.com/nim-lang/Nim/blob/devel/compiler/semcall.nim">semcall.nim</a></li>
<li>Template instantiation: <a href="https://github.com/nim-lang/Nim/blob/devel/compiler/evaltempl.nim">evaltempl.nim</a></li>
<li>Semantic analysis of hygienic templates: <a href="https://github.com/nim-lang/Nim/blob/devel/compiler/semtempl.nim">semtempl.nim</a></li>
<li>Semantic checking of type declarations: <a href="https://github.com/nim-lang/Nim/blob/devel/compiler/semtypes.nim">semtypes.nim</a></li>
<li>Generic instantiation: <a href="https://github.com/nim-lang/Nim/blob/devel/compiler/semgnrc.nim">semgnrc.nim</a></li>
<li>Semantic analysis of expressions: <a href="https://github.com/nim-lang/Nim/blob/devel/compiler/semexprs.nim">semexprs.nim</a></li>
<li>Semantic analysis of statements: <a href="https://github.com/nim-lang/Nim/blob/devel/compiler/semstmts.nim">semstmts.nim</a></li>
</ul>
<h3>How does it work?</h3>
<p>Templates in Nim work by replacing code: the template call is substituted by the template body in the call location.</p>
<pre><code class="language-nim">template foo =
  echo &quot;Add the echo line&quot;

proc bar =
  foo
  foo

bar()
</code></pre>
<p>When the compiler finds a call to <code>foo</code>, instead of inserting a procedure call like with a traditional proc, it replace the call with the body of the template. In our small example, <code>bar</code> proc is identical as if the programmer had written it like:</p>
<pre><code class="language-nim">proc bar =
  echo &quot;Add the echo line&quot;
  echo &quot;Add the echo line&quot;
</code></pre>
<h3>How to select the right template?</h3>
<p>Usually, like for proc overloading, the template that will be selected for application will be selected based on its parameters.</p>
<pre><code class="language-nim">template foo =
  echo &quot;Add the echo line&quot;

template foo(i: int) =
  echo &quot;i = &quot; &amp; $i

proc bar(i: int) =
  foo(i)
  foo

bar 3
</code></pre>
<p>Now, <code>bar</code> is seen by the compiler as if the developer had written:</p>
<pre><code class="language-nim">proc bar(i: int) =
  echo &quot;i = &quot; &amp; $i
  echo &quot;Add the echo line&quot;
</code></pre>
<h3>Scope</h3>
<p>As a template application is not a call but a source code substitution, no new scope is created by the compiler. More generally, the compiler can't check the validity of the code of a template definition apart from being syntactically correct by the parser. The resulting code will be checked when the template body is applied to instantiation site. As a consequence, the validity of a template body will not be checked by the compiler as long as the template is not used.</p>
<pre><code class="language-nim">template foo =
  echo 1 + a

proc fizz =
  let a = 3
  foo()

fizz()
</code></pre>
<p>Variable <code>a</code> is not defined nor known in template <code>foo</code> but the compiler accepts the template definition as valid as long as it respects Nim syntax.</p>
<h3>Redefinition</h3>
<p>Contrarily to procs, templates can be redefined in source code and so the following code is valid and prints <code>1</code> and <code>2</code>.</p>
<pre><code class="language-nim">template foo: int =
  1
  
echo foo()
  
template foo: int =
  2
  
echo foo()
</code></pre>
<p>You can see that it can lead to nasty bugs when you redefine inadvertently a template because of code move. A template definition is known by the compiler by its last definition at the moment it is used.</p>
<pre><code class="language-nim">template foo: int =
  1
  
template bar: int =
  foo()
  
template foo: int =
  2

echo bar()
</code></pre>
<p>So the code above prints <code>1</code> as only the first definition is known. You can change this behaviour to delay symbol resolution by the compiler with the <a href="https://nim-lang.org/docs/manual.html#generics-mixin-statement"><code>mixin</code> statement</a>, but now the compiler raises an error as it sees two <code>foo</code> symbols defined!</p>
<pre><code class="language-nim">template foo: int =
  1
  
template bar: int =
  mixin foo
  foo()
  
template foo: int =
  2

echo bar()
</code></pre>
<pre><code class="language-sh">Error: ambiguous call; both in.foo() [declared in /usercode/in.nim(1, 10)] and in.foo() [declared in /usercode/in.nim(8, 10)] match for: ()
</code></pre>
<h3>Return value</h3>
<p>The last instruction of the templates defines its &quot;return&quot; value. The template is not really returning a value as its whole body text is replaced in the call site. It just signifies to the compiler that the last instruction of the whole text replacement is to be of the type given by the return type of the template definition.</p>
<pre><code class="language-nim">template foo: int =
  1 + a

proc fizz =
  let a = 3
  let i = foo()
  echo &quot;i = &quot;, i

fizz()
</code></pre>
<h3>Debugging templates</h3>
<p>Debugging templates can be more challenging that debugging traditional procedures because the error messages can be located at the template application site but be related to the template definition.</p>
<pre><code class="language-nim">template foo: int =
  1 + a

proc fizz =
  let a = false
  let i = foo()
  echo &quot;i = &quot;, i

fizz()
</code></pre>
<pre><code>$ nim c -r tmpl
Hint: used config file '.../config/nim.cfg' [Conf]
Hint: used config file '.../config/config.nims' [Conf]
....
.../tmpl.nim(6, 14) Error: type mismatch: got &lt;int literal(1), bool&gt;
but expected one of: 
proc `+`(x, y: float): float
  first type mismatch at position: 2
  required type for y: float
  but expression 'a' is of type: bool
...
proc `+`(x: int8): int8
  first type mismatch at position: 2
  extra argument given
1 other mismatching symbols have been suppressed; compile with --showAllMismatches:on to see them

expression: 1 + a
</code></pre>
<p>The first debugging rule is <em>If you don't really read a template, use a proc instead</em>. You'll face less compiler bugs.</p>
<p>A constant cause of bug in templates is re-using a parameter name as a variable, forgetting that all occurrences of the parameter name are renamed in the template body.</p>
<p>One can sometimes use <code>expandMacros</code> to get a view of the template's definition with parameters substituted, particularly in the case of template defining other templates.</p>
<h3>Template hygiene</h3>
<p>When a template is applied, its parameters are substituted by the argument values of the instantiation site.</p>
<pre><code class="language-nim">template foo(a: int) =
  echo &quot;a + i = &quot;, a + i

proc fizz =
  let a = 2
  let i = 3
  foo(i)

fizz()
</code></pre>
<p>The variable <code>a</code> in the proc <code>fizz</code> is not the same as the one in the template <code>foo</code>. When applying the template, the compiler renames the template arguments before applying them. The previous code is similar to:</p>
<pre><code class="language-nim">proc fizz =
  let a = 2
  let i = 3
  echo &quot;a + i = &quot;, a`gensym12345 + i

fizz()
</code></pre>
<p>Taking an example from compiler <a href="https://github.com/nim-lang/Nim/blob/devel/compiler/semtempl.nim#L12-L27">semtempl.nim</a>:</p>
<pre><code class="language-nim">template `||` (a, b: untyped): untyped =
  let aa = a
  if aa: aa else: b

var
  a, b: bool
echo a || b || a
</code></pre>
<p>First, using the temporary variable <code>aa</code> with the parameter <code>a</code> value prevent multiple evaluations of <code>a</code>, because <code>a</code> value when the template is instantiated will be replicated as-is from the template definition into the code.</p>
<p>Then, when <code>a || b || a</code> is evaluated, the <code>a</code> here is not the same as the one in the template arguments, and the template argument <code>a</code> will need to be renamed with a temporary name to prevent clashes. This will need to be done multiple times as the template is invoked twice. The resulting code will look like:</p>
<pre><code class="language-nim">var
  a, b: bool
echo (let aa2 = (let aa1 = a; if aa1: aa1 else b); if aa2: aa2 else: a)
</code></pre>
<p>This renaming of template variables is some sort of hygiene to prevent name clashes and is done automatically by Nim compiler while the programmer must prevent multiple unwanted parameter evaluations.</p>
<h3>Dirty templates</h3>
<p>TODO</p>
<h3>Template parameters</h3>
<p>As already explained, templates are selected on there name and then checked for their parameters against the type of the arguments on the call site. In case of ambiguity when multiple templates definitions exist with the same name, parameters are used to select the right template definition to apply in a process similar to proc overloading.</p>
<p>This template application overloading works only for functional parameters but does not work for generic parameters (TODO: at least, to my understanding, it should not work when the generic variables are not typed).</p>
<pre><code class="language-nim">template foo(i: int): string =
  &quot;In foo of int&quot;

template foo(s: string): string =
  &quot;In foo of string&quot;

echo foo(3)
echo foo(&quot;Yoo!&quot;)
</code></pre>
<p>will print</p>
<pre><code>$ nim c -r tmpl.nim
...
In foo of int
In foo of string
</code></pre>
<p>Beware that the type of template parameters is only used during signature matching (i.e. the compiler just checks that types can match modulo implicit conversions) but somehow is lost when template is instantiated so for instance <code>template foo[T](a, b: T)</code> could be invoked with <code>a</code> and <code>b</code> being respectively <code>int</code> and <code>int32</code>. This could be considered a bug (https://github.com/nim-lang/Nim/issues/11794).</p>
<h3>Parameters evaluation</h3>
<p>Parameters are not evaluated by templates but are substituted in the source code when the template is applied. The following example</p>
<pre><code class="language-nim">proc incr(x: var int): int =
  inc x
  result = x

template foo(i: int): int =
  i + i

var a = 1
echo &quot;foo = &quot;, foo(incr a)
echo &quot;a = &quot;, a
</code></pre>
<p>prints the results</p>
<pre><code>foo = 5
a = 3
</code></pre>
<p>Because <code>foo</code> is a template, its application <code>foo(incr a)</code> must not be considered as a traditional procedure call (though it looks like one) and its parameter function call has been evaluated twice! Always keep always in mind that templates are text substitution in the source code. This example is rewritten like the following by the Nim compiler:</p>
<pre><code class="language-nim">proc incr(x: var int): int =
  inc x
  result = x

var a = 1
echo &quot;foo = &quot;, (incr a) + (incr a)
echo &quot;a = &quot;, a
</code></pre>
<p>This applies to generic parameters too. While in a procedure, a generic parameter instantiated by a type in a call can be considered as defining another instance of the procedure for that type by the compiler, this mechanism does not apply with templates. In a template application, the generic parameter is instantiated like the other parameters and the template definition is recopied in the source code.</p>
<h3>Var parameters and templates</h3>
<p>Though it does not make sense to declare template's parameters as <code>var</code>, Nim compiler accepts them and the type system checks that types and variability hold.</p>
<pre><code class="language-nim">template foo(x: var int): int =
  x = x + 1
  x

template foo(x: int): int =
  x + 3
  
var x = 1
echo &quot;foo(x) = &quot;, foo(x), &quot;, foo(1) = &quot;, foo(1)
echo &quot;x = &quot;, x
</code></pre>
<p>prints the expected result</p>
<pre><code>foo(x) = 2, foo(1) = 4
x = 2
</code></pre>
<h3>When are templates invoked?</h3>
<p>Compilation in Nim occurs in multiple passes. At first, the source code is parsed for valid syntax but the types are not checked yet. Then in a second pass, types are checked in what is call semantic analysis. A special type of templates (and macros) can be used to be invoked during the first pass. This allows to process source text that is not valid Nim code, for instance to write a DSL or code rewrite functions.</p>
<p>When a template parameter is declared as <code>untyped</code>, its type is not checked by the compiler.</p>
<p>If all parameters of a template are <code>untyped</code>, then the template is applied during the first pass of compilation. These are called immediate templates.</p>
<p>You can mix <code>untyped</code> and <code>typed</code> parameters. In that case, the template is evaluated during the semantic phase.</p>
<p>But when is invoked a template without parameters? If the return type is <code>untyped</code>, it makes sure the replacement is done before any semantic or type resolution. If the template has no return type, it is probably applied during the first phase.</p>
<h3>Untyped parameters</h3>
<p><code>untyped</code> parameters are useful to capture source code text before semantic analysis, that is text that respects Nim syntax but could be invalid. Templates are too limited in their features to process these type of parameters and macros are more suited to process these parameters. But templates can still use <code>untyped</code> parameters, for instance to define block-like keywords.</p>
<pre><code class="language-nim">TODO TO BE COMPLETED....
</code></pre>
<h3>Scope again</h3>
<p>According to <a href="https://nim-lang.org/docs/manual.html#templates-symbol-binding-in-templates">Nim manual</a>, templates as <em>hygienic macro</em> open a new scope but this is not clear. Why is the following code working in that case?</p>
<pre><code class="language-nim">template declareInScope(x: untyped, t: typedesc) =
  var x: t

declareInScope(a, int)
a = 3
echo &quot;a=&quot;, a
</code></pre>
<p>The <a href="https://nim-lang.org/docs/manual.html#templates-typed-vs-untyped-parameters">reason</a> is that the <code>declareInScope</code> template is <code>untyped</code> because <code>x</code> argument is <code>untyped</code> and is evaluated before type checking.</p>
<p>As <code>untyped</code> templates can accept invalid syntax that is replaced in the AST of the code being analysed by the compiler, I don't think we can talk of scope for <code>untyped</code> templates with the same meaning as for procs.</p>
<h3>Back on return type</h3>
<p>If a template is given no return type and it contains no <code>untyped</code> argument, it is considered <code>typed</code> of <code>void</code> type. Return type of <code>typed</code> templates is checked by the compiler.</p>
<pre><code class="language-nim">template foo: untyped =
  &quot;untyped foo&quot;

template bar =
  &quot;bar&quot;
    
template fizz: string =
  &quot;void fizz&quot;
  
template bu: void =
  echo &quot;boo!&quot;
    
echo foo()
echo bar()
echo fizz()
bu()
</code></pre>
<h3>Recursion in templates</h3>
<p>The compiler expands templates at compile time. When encountering a call, it checks if it's a template (or macro or proc but we're talking about templates here) and if it's the case, it replaces the call by the template body source after renaming parameters. And then it parses again the new source code  to see if another template substitution must be applied. And again and again until no more templates can be applied.
So can we write recursive templates? Based on these explanations, no. The compiler would go into an infinite loop.</p>
<pre><code class="language-nim">template foo(i: int) =
  echo &quot;i is now = &quot; &amp; $i
  if i &gt; 0:
    foo(i - 1)

proc fizz =
  foo(5)

fizz()
</code></pre>
<pre><code>$ nim c tmpl
Hint: used config file '.../config/nim.cfg' [Conf]
Hint: used config file '.../config.nims' [Conf]
....
.../tmpl.nim(7, 6) template/generic instantiation of `foo` from here
.../tmpl.nim(4, 8) template/generic instantiation of `foo` from here
.../tmpl.nim(3, 8) Error: template instantiation too nested
</code></pre>
<p>But the compiler evaluates <code>when</code> cases at compile-time when expanding template invocation and recursive templates can be evaluated in Nim virtual machine!</p>
<pre><code class="language-nim">template foo(i: int) =
  echo &quot;i is now = &quot; &amp; $i
  when i &gt; 0:
    foo(i - 1)

proc fizz =
  foo(5)

fizz()
</code></pre>
<p>prints</p>
<pre><code>i is now = 5
i is now = 4
i is now = 3
i is now = 2
i is now = 1
i is now = 0
</code></pre>
<p>showing that the recursive template has been invoked 6 times.</p>
<p>There's a constant defined to prevent endless recursion in template instantiation: https://github.com/nim-lang/Nim/blob/devel/compiler/evaltempl.nim#L143</p>
<h3>Co-recursion</h3>
<pre><code class="language-nim">import macros

template foo(a: int): int =
  a * bar(a - 1)

template bar(a: int): int =
  when a == 1:
    1
  else:
    foo(a)
  
expandMacros:
  echo foo(5)
</code></pre>
<p>prints the factorial of 5 calculated at compile time. So templates can be co-recursive too.</p>
<h3>Typed arguments</h3>
<p>As explained in <a href="https://nim-lang.org/docs/manual.html#templates-passing-a-code-block-to-a-template">Nim manual</a>, symbols in a template with <code>typed</code> arguments must be resolved when the template is instantiated. And this can create strange errors (see example in Manual).</p>
<h3>Templates and variable arguments</h3>
<p>Templates accept <code>varargs[]</code> arguments but they can't be processed by the templates (i.e, the template can't scan the <code>varargs</code> content).</p>
<h3>Templates and generic parameters</h3>
<p>Like we already explained, templates are not procedure calls but source text replacement, after selection of the correct instance of template definition based on its name and parameter types. Each parameter is substituted by its expression on the template instantiation site. The same process occurs for template generic parameters. <a href="https://github.com/nim-lang/Nim/blob/devel/compiler/semcall.nim#L525-L526">Generic parameters are considered like functional parameters for template selection</a> because there is no type instantiation like what occurs with procs. When they are unconstrained by a type, they are considered <code>untyped</code>.</p>
<pre><code class="language-nim">template foo[N](a: N): int =
  len(a)

echo &quot;Length of seq[int] = &quot;, foo(@[1, 2, 3])
echo &quot;Length of seq[string] = &quot;, foo(@[&quot;I&quot;, &quot;have&quot;, &quot;a&quot;, &quot;dream&quot;])
echo &quot;Length of string = &quot;, foo(&quot;When I was young&quot;)
</code></pre>
<h3>Templates and static parameters</h3>
<p>Static parameters are constant expressions known at compile time. Static parameters must be constant expressions when a template is invoked. They can be used in generic parameters as well as functional parameters.</p>
<h4>BUG:</h4>
<p>When both a static and non-static generic parameter template definitions are available, the most constrained one will be selected. The following code</p>
<pre><code class="language-nim">template foo[N: string]: untyped =
  &quot;foo string&quot;

template foo[N: static string]: untyped =
  &quot;foo static string&quot;

template foo[N]: untyped =
  &quot;foo generic&quot;

echo foo[&quot;wellwell&quot;]()
</code></pre>
<p>does not compile with Nim 1.3.5 but should print <code>foo static string</code>.</p>
<h3>Executing code in templates</h3>
<p>Can we execute code in template? For instance, would it be possible to have a template that expands to some text code when a condition is defined, and to another text code if not? What about:</p>
<pre><code class="language-nim">import macros

template foo(x: int): string =
  when x mod 7 == 0:
    &quot;buzzle&quot;
  else:
    $x

expandMacros:
  echo foo(6)
  echo foo(7)
  echo foo(8)
</code></pre>
<p>This seems to work, as if the <code>when</code> branch was executed when the template is expanded and template invocation is substituted by template body, but in fact this execution is done at the compilation phase after all templates invocations. The corresponding code expansion is not</p>
<pre><code class="language-nim">echo $6
echo &quot;buzzle&quot;
echo $7
</code></pre>
<p>but</p>
<pre><code class="language-nim">echo (when 6 mod 7 == 0: &quot;buzzle&quot; else: $6)
echo (when 7 mod 7 == 0: &quot;buzzle&quot; else: $7)
echo (when 8 mod 7 == 0: &quot;buzzle&quot; else: $8)
</code></pre>
<p>So the answer is <em>I don't know</em>. It seems that <code>when</code> clauses are executed in Nim VM while expanding templates (look at the recursion examples). So probably <code>template</code> + <code>when</code> can be used to create <em>dynamic</em> templates...</p>
<h3>Lookup for symbols in template</h3>
<p>How does a template find symbols in its definition? For <a href="https://forum.nim-lang.org/t/6560">example from Nim Forum</a>,</p>
<pre><code class="language-nim">proc x[T](i: T) =
  echo &quot;generic x&quot;

template temp(a: int) =
  x(a)

proc x(i: int) =
  echo &quot;specific x&quot;

temp(14)
</code></pre>
<p>prints <code>generic x</code> while</p>
<pre><code class="language-nim">template temp(a: int) =
  x(a)

proc x[T](i: T) =
  echo &quot;generic x&quot;

proc x(i: int) =
  echo &quot;specific x&quot;

temp(14)
</code></pre>
<p>prints <code>specific x</code>. In one case, the generic proc was selected while the specific proc was in the other case, only by changing the position of the template definition in the source code.</p>
<p>The reason is that if only one proc with the name of the symbol is defined when the template definition is evaluate, as seen by Nim compiler parsing the source, the symbol will be considered to be a closed symbol (<code>closedSymChoice</code>) while it is considered as an open symbol (<code>openSymChoice</code>). The programmer can control this behaviour and force the template to select the most specific proc, independently of the template position in the source code, by using the <code>mixin</code> <a href="https://nim-lang.org/docs/manual.html#generics-mixin-statement">keyword</a>.</p>
<pre><code class="language-nim">template temp(a: int) =
  mixin x
  x(a)
</code></pre>
<p>Reversely, to force the template to bind a symbol early, the <code>bind</code> <a href="https://nim-lang.org/docs/manual.html#generics-bind-statement">keyword</a> can be used. But a bind is rarely useful because symbol binding from the definition scope is the default.</p>
<pre><code class="language-nim">template temp(a: int) =
  bind x
  x(a)
</code></pre>
<h3>Templates and export</h3>
<p>Can proc generated by templates made visible from outside the current module?</p>
<pre><code class="language-nim">template foo(procName: untyped) =
  proc procName*(x: int): int =
    result = x

foo(bar)

expandMacros:
  foo(fizz)
</code></pre>
<p>Note that there's a bug (https://github.com/nim-lang/Nim/issues/13828) with Nim 1.2 where visibility is not preserved when the template generates a <code>template</code> instead of a <code>proc</code>.</p>
<h3>Why do templates accept parameters without types?</h3>
<p>Why is the following syntax accepted by the compiler and what does it mean?</p>
<pre><code class="language-nim">import macros

template foo(procName) =
  proc procName(x: int): int =
    result = x

expandMacros:
  foo(bar)
</code></pre>
<p>TODO: I suppose no-types parameters are considered <code>untyped</code>. While templates without return type are probably considered <code>void</code>...
All the following definitions are equivalent from a compilation point of view:</p>
<pre><code class="language-nim">template foo(procName: untyped): untyped =
  proc procName(x: int): int =
    result = x
</code></pre>
<pre><code class="language-nim">template foo(procName: untyped) =
  proc procName(x: int): int =
    result = x
</code></pre>
<pre><code class="language-nim">template foo(procName) =
  proc procName(x: int): int =
    result = x
</code></pre>
<h3>You can't discard a template</h3>
<p>Well, at least it makes no consistent sense as a template invocation is not a function call. So even if a template instantiation site looks like a proc call, using <code>discard</code> is non-predictable as it depends on the template body definition.</p>
<pre><code class="language-nim">template foo =
  echo &quot;Biz&quot;
  1
  
discard foo()
</code></pre>
<p>Does the <code>discard</code> applies on both <code>echo &quot;Biz&quot;</code> and <code>1</code>, or only on <code>1</code>?</p>
<h3>Templates and result variable</h3>
<p>Why the <code>result</code> variable makes no sense with templates... If <code>result</code> existed for templates, the following template</p>
<pre><code class="language-nim">template foo(a: int): int =
  let b = a + 1
  echo b
  b
</code></pre>
<p>would be written like</p>
<pre><code class="language-nim">template foo(a: int): int =
  result = `let b = a + 1; echo b; b`
</code></pre>
<p>The whole body text of the template would be assigned to the <code>result</code> automatic variable. And you can see that the type of <code>result</code> (<code>void</code> or <code>untyped</code>?) does not necessarily match with the return type of the template (<code>int</code>).</p>
<h3>Templates on types</h3>
<p>Can a template be used in contexts other than proc calls? For instance, can we define a template for a type declaration?</p>
<pre><code>template foo: untyped =
  array[4, int]

var a: foo() = [0, 1, 2, 3]
echo &quot;a = &quot;, a
</code></pre>
<p>correctly prints <code>[0, 1, 2, 3]</code>.</p>
<p>Template expansion is not limited to statements context (as if a proc was evaluated) and can't be used in other contexts.</p>
<h3>Defining an iterator in a template</h3>
<p>Using a template to define an iterator is not as direct as for a proc. The first way is to use an inline closure iterator with no name:</p>
<pre><code class="language-nim">type
  Melon = object

template pairs*(m: Melon): tuple[key: int, val: int] =
  let iter = iterator(melon: Melon): tuple[key: int, val: int] =
    for i in a .. 10:
      yield (key: i, val: i * i)
  iter(m)

block:
  let a = 2
  let m = Melon()
  for k, v in m:
    echo &quot;k=&quot;, k, &quot;; v=&quot;, v
</code></pre>
<p>or without a closure iterator:</p>
<pre><code class="language-nim">type
  Melon = object

template pairs*(m: Melon): tuple[key: int, val: int] =
  (iterator(melon: Melon): tuple[key: int, val: int] =
    for i in a .. 10:
      yield (key: i, val: i * i))(m)

block:
  let a = 2
  let m = Melon()
  for k, v in m:
    echo &quot;k=&quot;, k, &quot;; v=&quot;, v
</code></pre>
<p>Reference: https://forum.nim-lang.org/t/6378</p>
<h3>Term rewriting templates</h3>
<p>TODO</p>
<h3>When to use templates?</h3>
<p>The main rule to using templates is <strong>If you can do without templates, either with procs or macros, don't use templates. You'll save on nasty bugs and debugging trouble.</strong></p>
<p>Templates are useful to:</p>
<ol>
<li>Reduce the size of a block of code, when some text parts can be factorized in a template. For instance, in an external GUI library, all API calls share a <code>ctx</code> parameter that keeps the context of the call. Instead of forcing the user to specify this argument in each call, the library author has defined templates that add this parameter. To the library user, templates invocations seem similar to proc calls, but with a lighter syntax.</li>
</ol>
<pre><code class="language-nim">template newCanvas(body: untyped) =
  block:
    let ctx = GUI_newContext()
    body

template drawLine(p1, p2: Point) =
  GUI_drawLine(ctx, p1, p2)

# Now in the code with a nice syntax
newCanvas:
  drawLine x1, x2
</code></pre>
<ol start="2">
<li>
<p>Optimization, particularly when using term rewriting templates.</p>
</li>
<li>
<p>Creation of simple DSL
For instance, after using a database connection, the user must return it to the connection pool. A template can automate this simple pattern and hide the <em>complexity</em> to the user:</p>
</li>
</ol>
<pre><code class="language-nim">template execSQL(sql: string): Rows =
  block:
    let conn = getConnection(db)
    let res = conn.sqlExecute(sql)
    releaseConnection(conn)
    res

# ... in code
t = execSQL &quot;select * from ...&quot; 
</code></pre>
<ol start="4">
<li>When some part of the code must be evaluated at compilation time.</li>
</ol>
<h3>Consequences of using templates</h3>
<ol>
<li>Even if code can be smaller when using templates, executable will be probably larger because template code is replicated when templates are instantiated, compared when using procs.</li>
<li>When used judiciously, templates can make faster executable: no procs call stack and parameters management; applying optimizations with term rewriting.</li>
<li>More difficult to debug. Templates are not seen by debuggers...</li>
</ol>
<h2>Issues with templates</h2>
<h3>Generic and templates</h3>
<p>https://github.com/nim-lang/Nim/issues/13527</p>
<h2>Common bugs when using templates</h2>
<ol>
<li>Parameter evaluated multiple times</li>
</ol>
<pre><code class="language-nim">proc incr(x: var int): int =
  inc x
  result = x

template foo(i: int): int =
  i + i

var a = 1
echo &quot;foo = &quot;, foo(incr a)
echo &quot;a = &quot;, a
</code></pre>
<ol start="2">
<li>Parameter hygiene in template or proc generating template</li>
</ol>
<pre><code class="language-nim">import macros

template foo(x: int; y: int) =
  proc bar(x: int): int =
    result = y

expandMacros:
  foo(1, 2)
</code></pre>
<p>results in <code>Error: identifier expected, but found '1'</code>.
Here, <code>x</code> argument in <code>bar</code> has been renamed by <a href="https://nim-lang.org/docs/manual.html#templates-hygiene-in-templates">template arguments hygiene</a> when template <code>foo</code> is instantiated. This can be seen in the <code>expandMacros</code> result displayed when compiling, after changing <code>x</code> in <code>bar</code> for <code>a</code>.</p>
<ol start="3">
<li>Obscure error message</li>
</ol>
<pre><code class="language-nim">template foo(a: int, b: int) =
  template bar(a: int): int =
    a == b

foo(1, 2)
</code></pre>
<p>results in <code>Error: identifier expected, but found '1'</code>.</p>
</main>
<footer>
<hr>
<div class="nb-box">
  <span><span class="nb-small">made with <a href="https://pietroppeter.github.io/nimib/">nimib üê≥</a></span></span>
  <span></span>
  <span><button class="nb-small" id="show" onclick="toggleSourceDisplay()">Show Source</button></span>
</div>
</footer>
<section id="source">
<pre><code class="nim hljs"><span class="hljs-keyword">import</span> strformat, strutils
<span class="hljs-keyword">import</span> nimib
<span class="hljs-keyword">import</span> nimoji

nbInit

nbText:<span class="hljs-string">&quot;&quot;&quot;
## Templates

This page is a draft for brainstorming about Nim templates, before writing a [RFC](https://github.com/nim-lang/RFCs). The goal is to complete the [specification of templates](https://nim-lang.org/docs/manual.html#templates) and eventually complete the implementation in the compiler.

#### Compiler sources
Some sources related to templates...
* Proc and template calls: [semcall.nim](https://github.com/nim-lang/Nim/blob/devel/compiler/semcall.nim)
* Template instantiation: [evaltempl.nim](https://github.com/nim-lang/Nim/blob/devel/compiler/evaltempl.nim)
* Semantic analysis of hygienic templates: [semtempl.nim](https://github.com/nim-lang/Nim/blob/devel/compiler/semtempl.nim)
* Semantic checking of type declarations: [semtypes.nim](https://github.com/nim-lang/Nim/blob/devel/compiler/semtypes.nim)
* Generic instantiation: [semgnrc.nim](https://github.com/nim-lang/Nim/blob/devel/compiler/semgnrc.nim)
* Semantic analysis of expressions: [semexprs.nim](https://github.com/nim-lang/Nim/blob/devel/compiler/semexprs.nim)
* Semantic analysis of statements: [semstmts.nim](https://github.com/nim-lang/Nim/blob/devel/compiler/semstmts.nim)

### How does it work?
Templates in Nim work by replacing code: the template call is substituted by the template body in the call location.
```nim
template foo =
  echo &quot;Add the echo line&quot;

proc bar =
  foo
  foo

bar()
```
When the compiler finds a call to `foo`, instead of inserting a procedure call like with a traditional proc, it replace the call with the body of the template. In our small example, `bar` proc is identical as if the programmer had written it like:
```nim
proc bar =
  echo &quot;Add the echo line&quot;
  echo &quot;Add the echo line&quot;
```

### How to select the right template?
Usually, like for proc overloading, the template that will be selected for application will be selected based on its parameters.
```nim
template foo =
  echo &quot;Add the echo line&quot;

template foo(i: int) =
  echo &quot;i = &quot; &amp; $i

proc bar(i: int) =
  foo(i)
  foo

bar 3
```
Now, `bar` is seen by the compiler as if the developer had written:
```nim
proc bar(i: int) =
  echo &quot;i = &quot; &amp; $i
  echo &quot;Add the echo line&quot;
```

### Scope
As a template application is not a call but a source code substitution, no new scope is created by the compiler. More generally, the compiler can't check the validity of the code of a template definition apart from being syntactically correct by the parser. The resulting code will be checked when the template body is applied to instantiation site. As a consequence, the validity of a template body will not be checked by the compiler as long as the template is not used.
```nim
template foo =
  echo 1 + a

proc fizz =
  let a = 3
  foo()

fizz()
```
Variable `a` is not defined nor known in template `foo` but the compiler accepts the template definition as valid as long as it respects Nim syntax.

### Redefinition
Contrarily to procs, templates can be redefined in source code and so the following code is valid and prints `1` and `2`.
```nim
template foo: int =
  1
  
echo foo()
  
template foo: int =
  2
  
echo foo()
```

You can see that it can lead to nasty bugs when you redefine inadvertently a template because of code move. A template definition is known by the compiler by its last definition at the moment it is used.
```nim
template foo: int =
  1
  
template bar: int =
  foo()
  
template foo: int =
  2

echo bar()
```
So the code above prints `1` as only the first definition is known. You can change this behaviour to delay symbol resolution by the compiler with the [`mixin` statement](https://nim-lang.org/docs/manual.html#generics-mixin-statement), but now the compiler raises an error as it sees two `foo` symbols defined!
```nim
template foo: int =
  1
  
template bar: int =
  mixin foo
  foo()
  
template foo: int =
  2

echo bar()
```
```sh
Error: ambiguous call; both in.foo() [declared in /usercode/in.nim(1, 10)] and in.foo() [declared in /usercode/in.nim(8, 10)] match for: ()
```

### Return value
The last instruction of the templates defines its &quot;return&quot; value. The template is not really returning a value as its whole body text is replaced in the call site. It just signifies to the compiler that the last instruction of the whole text replacement is to be of the type given by the return type of the template definition.
```nim
template foo: int =
  1 + a

proc fizz =
  let a = 3
  let i = foo()
  echo &quot;i = &quot;, i

fizz()
```

### Debugging templates
Debugging templates can be more challenging that debugging traditional procedures because the error messages can be located at the template application site but be related to the template definition.
```nim
template foo: int =
  1 + a

proc fizz =
  let a = false
  let i = foo()
  echo &quot;i = &quot;, i

fizz()
```
```
$ nim c -r tmpl
Hint: used config file '.../config/nim.cfg' [Conf]
Hint: used config file '.../config/config.nims' [Conf]
....
.../tmpl.nim(6, 14) Error: type mismatch: got &lt;int literal(1), bool&gt;
but expected one of: 
proc `+`(x, y: float): float
  first type mismatch at position: 2
  required type for y: float
  but expression 'a' is of type: bool
...
proc `+`(x: int8): int8
  first type mismatch at position: 2
  extra argument given
1 other mismatching symbols have been suppressed; compile with --showAllMismatches:on to see them

expression: 1 + a
```

The first debugging rule is *If you don't really read a template, use a proc instead*. You'll face less compiler bugs.

A constant cause of bug in templates is re-using a parameter name as a variable, forgetting that all occurrences of the parameter name are renamed in the template body.

One can sometimes use `expandMacros` to get a view of the template's definition with parameters substituted, particularly in the case of template defining other templates.

### Template hygiene
When a template is applied, its parameters are substituted by the argument values of the instantiation site.
```nim
template foo(a: int) =
  echo &quot;a + i = &quot;, a + i

proc fizz =
  let a = 2
  let i = 3
  foo(i)

fizz()
```
The variable `a` in the proc `fizz` is not the same as the one in the template `foo`. When applying the template, the compiler renames the template arguments before applying them. The previous code is similar to:
```nim
proc fizz =
  let a = 2
  let i = 3
  echo &quot;a + i = &quot;, a`gensym12345 + i

fizz()
```

Taking an example from compiler [semtempl.nim](https://github.com/nim-lang/Nim/blob/devel/compiler/semtempl.nim#L12-L27):
```nim
template `||` (a, b: untyped): untyped =
  let aa = a
  if aa: aa else: b

var
  a, b: bool
echo a || b || a
```
First, using the temporary variable `aa` with the parameter `a` value prevent multiple evaluations of `a`, because `a` value when the template is instantiated will be replicated as-is from the template definition into the code.

Then, when `a || b || a` is evaluated, the `a` here is not the same as the one in the template arguments, and the template argument `a` will need to be renamed with a temporary name to prevent clashes. This will need to be done multiple times as the template is invoked twice. The resulting code will look like:
```nim
var
  a, b: bool
echo (let aa2 = (let aa1 = a; if aa1: aa1 else b); if aa2: aa2 else: a)
```
This renaming of template variables is some sort of hygiene to prevent name clashes and is done automatically by Nim compiler while the programmer must prevent multiple unwanted parameter evaluations.

### Dirty templates
TODO

### Template parameters
As already explained, templates are selected on there name and then checked for their parameters against the type of the arguments on the call site. In case of ambiguity when multiple templates definitions exist with the same name, parameters are used to select the right template definition to apply in a process similar to proc overloading.

This template application overloading works only for functional parameters but does not work for generic parameters (TODO: at least, to my understanding, it should not work when the generic variables are not typed).

```nim
template foo(i: int): string =
  &quot;In foo of int&quot;

template foo(s: string): string =
  &quot;In foo of string&quot;

echo foo(3)
echo foo(&quot;Yoo!&quot;)
```
will print
```
$ nim c -r tmpl.nim
...
In foo of int
In foo of string
```

Beware that the type of template parameters is only used during signature matching (i.e. the compiler just checks that types can match modulo implicit conversions) but somehow is lost when template is instantiated so for instance `template foo[T](a, b: T)` could be invoked with `a` and `b` being respectively `int` and `int32`. This could be considered a bug (https://github.com/nim-lang/Nim/issues/11794).

### Parameters evaluation
Parameters are not evaluated by templates but are substituted in the source code when the template is applied. The following example
```nim
proc incr(x: var int): int =
  inc x
  result = x

template foo(i: int): int =
  i + i

var a = 1
echo &quot;foo = &quot;, foo(incr a)
echo &quot;a = &quot;, a
```
prints the results
```
foo = 5
a = 3
```
Because `foo` is a template, its application `foo(incr a)` must not be considered as a traditional procedure call (though it looks like one) and its parameter function call has been evaluated twice! Always keep always in mind that templates are text substitution in the source code. This example is rewritten like the following by the Nim compiler:
```nim
proc incr(x: var int): int =
  inc x
  result = x

var a = 1
echo &quot;foo = &quot;, (incr a) + (incr a)
echo &quot;a = &quot;, a
```

This applies to generic parameters too. While in a procedure, a generic parameter instantiated by a type in a call can be considered as defining another instance of the procedure for that type by the compiler, this mechanism does not apply with templates. In a template application, the generic parameter is instantiated like the other parameters and the template definition is recopied in the source code.

### Var parameters and templates
Though it does not make sense to declare template's parameters as `var`, Nim compiler accepts them and the type system checks that types and variability hold.
```nim
template foo(x: var int): int =
  x = x + 1
  x

template foo(x: int): int =
  x + 3
  
var x = 1
echo &quot;foo(x) = &quot;, foo(x), &quot;, foo(1) = &quot;, foo(1)
echo &quot;x = &quot;, x
```
prints the expected result
```
foo(x) = 2, foo(1) = 4
x = 2
```

### When are templates invoked?
Compilation in Nim occurs in multiple passes. At first, the source code is parsed for valid syntax but the types are not checked yet. Then in a second pass, types are checked in what is call semantic analysis. A special type of templates (and macros) can be used to be invoked during the first pass. This allows to process source text that is not valid Nim code, for instance to write a DSL or code rewrite functions.

When a template parameter is declared as `untyped`, its type is not checked by the compiler.

If all parameters of a template are `untyped`, then the template is applied during the first pass of compilation. These are called immediate templates.

You can mix `untyped` and `typed` parameters. In that case, the template is evaluated during the semantic phase.

But when is invoked a template without parameters? If the return type is `untyped`, it makes sure the replacement is done before any semantic or type resolution. If the template has no return type, it is probably applied during the first phase.

### Untyped parameters
`untyped` parameters are useful to capture source code text before semantic analysis, that is text that respects Nim syntax but could be invalid. Templates are too limited in their features to process these type of parameters and macros are more suited to process these parameters. But templates can still use `untyped` parameters, for instance to define block-like keywords.
```nim
TODO TO BE COMPLETED....
```

### Scope again
According to [Nim manual](https://nim-lang.org/docs/manual.html#templates-symbol-binding-in-templates), templates as *hygienic macro* open a new scope but this is not clear. Why is the following code working in that case?
```nim
template declareInScope(x: untyped, t: typedesc) =
  var x: t

declareInScope(a, int)
a = 3
echo &quot;a=&quot;, a
```
The [reason](https://nim-lang.org/docs/manual.html#templates-typed-vs-untyped-parameters) is that the `declareInScope` template is `untyped` because `x` argument is `untyped` and is evaluated before type checking.

As `untyped` templates can accept invalid syntax that is replaced in the AST of the code being analysed by the compiler, I don't think we can talk of scope for `untyped` templates with the same meaning as for procs.

### Back on return type
If a template is given no return type and it contains no `untyped` argument, it is considered `typed` of `void` type. Return type of `typed` templates is checked by the compiler.
```nim
template foo: untyped =
  &quot;untyped foo&quot;

template bar =
  &quot;bar&quot;
    
template fizz: string =
  &quot;void fizz&quot;
  
template bu: void =
  echo &quot;boo!&quot;
    
echo foo()
echo bar()
echo fizz()
bu()
```

### Recursion in templates
The compiler expands templates at compile time. When encountering a call, it checks if it's a template (or macro or proc but we're talking about templates here) and if it's the case, it replaces the call by the template body source after renaming parameters. And then it parses again the new source code  to see if another template substitution must be applied. And again and again until no more templates can be applied.
So can we write recursive templates? Based on these explanations, no. The compiler would go into an infinite loop.
```nim
template foo(i: int) =
  echo &quot;i is now = &quot; &amp; $i
  if i &gt; 0:
    foo(i - 1)

proc fizz =
  foo(5)

fizz()
```

```
$ nim c tmpl
Hint: used config file '.../config/nim.cfg' [Conf]
Hint: used config file '.../config.nims' [Conf]
....
.../tmpl.nim(7, 6) template/generic instantiation of `foo` from here
.../tmpl.nim(4, 8) template/generic instantiation of `foo` from here
.../tmpl.nim(3, 8) Error: template instantiation too nested
```

But the compiler evaluates `when` cases at compile-time when expanding template invocation and recursive templates can be evaluated in Nim virtual machine!
```nim
template foo(i: int) =
  echo &quot;i is now = &quot; &amp; $i
  when i &gt; 0:
    foo(i - 1)

proc fizz =
  foo(5)

fizz()
```
prints
```
i is now = 5
i is now = 4
i is now = 3
i is now = 2
i is now = 1
i is now = 0
```
showing that the recursive template has been invoked 6 times.

There's a constant defined to prevent endless recursion in template instantiation: https://github.com/nim-lang/Nim/blob/devel/compiler/evaltempl.nim#L143

### Co-recursion
```nim
import macros

template foo(a: int): int =
  a * bar(a - 1)

template bar(a: int): int =
  when a == 1:
    1
  else:
    foo(a)
  
expandMacros:
  echo foo(5)
```
prints the factorial of 5 calculated at compile time. So templates can be co-recursive too.

### Typed arguments
As explained in [Nim manual](https://nim-lang.org/docs/manual.html#templates-passing-a-code-block-to-a-template), symbols in a template with `typed` arguments must be resolved when the template is instantiated. And this can create strange errors (see example in Manual).

### Templates and variable arguments
Templates accept `varargs[]` arguments but they can't be processed by the templates (i.e, the template can't scan the `varargs` content).

### Templates and generic parameters
Like we already explained, templates are not procedure calls but source text replacement, after selection of the correct instance of template definition based on its name and parameter types. Each parameter is substituted by its expression on the template instantiation site. The same process occurs for template generic parameters. [Generic parameters are considered like functional parameters for template selection](https://github.com/nim-lang/Nim/blob/devel/compiler/semcall.nim#L525-L526) because there is no type instantiation like what occurs with procs. When they are unconstrained by a type, they are considered `untyped`.
```nim
template foo[N](a: N): int =
  len(a)

echo &quot;Length of seq[int] = &quot;, foo(@[1, 2, 3])
echo &quot;Length of seq[string] = &quot;, foo(@[&quot;I&quot;, &quot;have&quot;, &quot;a&quot;, &quot;dream&quot;])
echo &quot;Length of string = &quot;, foo(&quot;When I was young&quot;)
```

### Templates and static parameters
Static parameters are constant expressions known at compile time. Static parameters must be constant expressions when a template is invoked. They can be used in generic parameters as well as functional parameters.

#### BUG:
When both a static and non-static generic parameter template definitions are available, the most constrained one will be selected. The following code
```nim
template foo[N: string]: untyped =
  &quot;foo string&quot;

template foo[N: static string]: untyped =
  &quot;foo static string&quot;

template foo[N]: untyped =
  &quot;foo generic&quot;

echo foo[&quot;wellwell&quot;]()
```
does not compile with Nim 1.3.5 but should print `foo static string`.

### Executing code in templates
Can we execute code in template? For instance, would it be possible to have a template that expands to some text code when a condition is defined, and to another text code if not? What about:
```nim
import macros

template foo(x: int): string =
  when x mod 7 == 0:
    &quot;buzzle&quot;
  else:
    $x

expandMacros:
  echo foo(6)
  echo foo(7)
  echo foo(8)
```

This seems to work, as if the `when` branch was executed when the template is expanded and template invocation is substituted by template body, but in fact this execution is done at the compilation phase after all templates invocations. The corresponding code expansion is not
```nim
echo $6
echo &quot;buzzle&quot;
echo $7
```
but
```nim
echo (when 6 mod 7 == 0: &quot;buzzle&quot; else: $6)
echo (when 7 mod 7 == 0: &quot;buzzle&quot; else: $7)
echo (when 8 mod 7 == 0: &quot;buzzle&quot; else: $8)
```
So the answer is *I don't know*. It seems that `when` clauses are executed in Nim VM while expanding templates (look at the recursion examples). So probably `template` + `when` can be used to create *dynamic* templates...

### Lookup for symbols in template
How does a template find symbols in its definition? For [example from Nim Forum](https://forum.nim-lang.org/t/6560),
```nim
proc x[T](i: T) =
  echo &quot;generic x&quot;

template temp(a: int) =
  x(a)

proc x(i: int) =
  echo &quot;specific x&quot;

temp(14)
```
prints `generic x` while
```nim
template temp(a: int) =
  x(a)

proc x[T](i: T) =
  echo &quot;generic x&quot;

proc x(i: int) =
  echo &quot;specific x&quot;

temp(14)
```
prints `specific x`. In one case, the generic proc was selected while the specific proc was in the other case, only by changing the position of the template definition in the source code.

The reason is that if only one proc with the name of the symbol is defined when the template definition is evaluate, as seen by Nim compiler parsing the source, the symbol will be considered to be a closed symbol (`closedSymChoice`) while it is considered as an open symbol (`openSymChoice`). The programmer can control this behaviour and force the template to select the most specific proc, independently of the template position in the source code, by using the `mixin` [keyword](https://nim-lang.org/docs/manual.html#generics-mixin-statement).
```nim
template temp(a: int) =
  mixin x
  x(a)
```

Reversely, to force the template to bind a symbol early, the `bind` [keyword](https://nim-lang.org/docs/manual.html#generics-bind-statement) can be used. But a bind is rarely useful because symbol binding from the definition scope is the default.
```nim
template temp(a: int) =
  bind x
  x(a)
```

### Templates and export
Can proc generated by templates made visible from outside the current module?
```nim
template foo(procName: untyped) =
  proc procName*(x: int): int =
	result = x

foo(bar)

expandMacros:
  foo(fizz)
```

Note that there's a bug (https://github.com/nim-lang/Nim/issues/13828) with Nim 1.2 where visibility is not preserved when the template generates a `template` instead of a `proc`.

### Why do templates accept parameters without types?
Why is the following syntax accepted by the compiler and what does it mean?
```nim
import macros

template foo(procName) =
  proc procName(x: int): int =
    result = x

expandMacros:
  foo(bar)
```

TODO: I suppose no-types parameters are considered `untyped`. While templates without return type are probably considered `void`...
All the following definitions are equivalent from a compilation point of view:
```nim
template foo(procName: untyped): untyped =
  proc procName(x: int): int =
    result = x
```
```nim
template foo(procName: untyped) =
  proc procName(x: int): int =
    result = x
```
```nim
template foo(procName) =
  proc procName(x: int): int =
    result = x
```

### You can't discard a template
Well, at least it makes no consistent sense as a template invocation is not a function call. So even if a template instantiation site looks like a proc call, using `discard` is non-predictable as it depends on the template body definition.
```nim
template foo =
  echo &quot;Biz&quot;
  1
  
discard foo()
```
Does the `discard` applies on both `echo &quot;Biz&quot;` and `1`, or only on `1`?

### Templates and result variable
Why the `result` variable makes no sense with templates... If `result` existed for templates, the following template
```nim
template foo(a: int): int =
  let b = a + 1
  echo b
  b
```
 would be written like
```nim
template foo(a: int): int =
  result = `let b = a + 1; echo b; b`
```
The whole body text of the template would be assigned to the `result` automatic variable. And you can see that the type of `result` (`void` or `untyped`?) does not necessarily match with the return type of the template (`int`).

### Templates on types
Can a template be used in contexts other than proc calls? For instance, can we define a template for a type declaration?
```
template foo: untyped =
  array[4, int]

var a: foo() = [0, 1, 2, 3]
echo &quot;a = &quot;, a
```
correctly prints `[0, 1, 2, 3]`.

Template expansion is not limited to statements context (as if a proc was evaluated) and can't be used in other contexts.

### Defining an iterator in a template
Using a template to define an iterator is not as direct as for a proc. The first way is to use an inline closure iterator with no name:
```nim
type
  Melon = object

template pairs*(m: Melon): tuple[key: int, val: int] =
  let iter = iterator(melon: Melon): tuple[key: int, val: int] =
    for i in a .. 10:
      yield (key: i, val: i * i)
  iter(m)

block:
  let a = 2
  let m = Melon()
  for k, v in m:
    echo &quot;k=&quot;, k, &quot;; v=&quot;, v
```
or without a closure iterator:
```nim
type
  Melon = object

template pairs*(m: Melon): tuple[key: int, val: int] =
  (iterator(melon: Melon): tuple[key: int, val: int] =
    for i in a .. 10:
      yield (key: i, val: i * i))(m)

block:
  let a = 2
  let m = Melon()
  for k, v in m:
    echo &quot;k=&quot;, k, &quot;; v=&quot;, v
```
Reference: https://forum.nim-lang.org/t/6378

### Term rewriting templates
TODO

### When to use templates?
The main rule to using templates is **If you can do without templates, either with procs or macros, don't use templates. You'll save on nasty bugs and debugging trouble.**

Templates are useful to:
1. Reduce the size of a block of code, when some text parts can be factorized in a template. For instance, in an external GUI library, all API calls share a `ctx` parameter that keeps the context of the call. Instead of forcing the user to specify this argument in each call, the library author has defined templates that add this parameter. To the library user, templates invocations seem similar to proc calls, but with a lighter syntax.
```nim
template newCanvas(body: untyped) =
  block:
    let ctx = GUI_newContext()
    body

template drawLine(p1, p2: Point) =
  GUI_drawLine(ctx, p1, p2)

# Now in the code with a nice syntax
newCanvas:
  drawLine x1, x2
```

2. Optimization, particularly when using term rewriting templates.

3. Creation of simple DSL
For instance, after using a database connection, the user must return it to the connection pool. A template can automate this simple pattern and hide the *complexity* to the user:
```nim
template execSQL(sql: string): Rows =
  block:
    let conn = getConnection(db)
    let res = conn.sqlExecute(sql)
    releaseConnection(conn)
    res

# ... in code
t = execSQL &quot;select * from ...&quot; 
```

4. When some part of the code must be evaluated at compilation time.

### Consequences of using templates
1. Even if code can be smaller when using templates, executable will be probably larger because template code is replicated when templates are instantiated, compared when using procs.
2. When used judiciously, templates can make faster executable: no procs call stack and parameters management; applying optimizations with term rewriting.
3. More difficult to debug. Templates are not seen by debuggers...


## Issues with templates

### Generic and templates
https://github.com/nim-lang/Nim/issues/13527

## Common bugs when using templates

1. Parameter evaluated multiple times
```nim
proc incr(x: var int): int =
  inc x
  result = x

template foo(i: int): int =
  i + i

var a = 1
echo &quot;foo = &quot;, foo(incr a)
echo &quot;a = &quot;, a
```

2. Parameter hygiene in template or proc generating template
```nim
import macros

template foo(x: int; y: int) =
  proc bar(x: int): int =
    result = y

expandMacros:
  foo(1, 2)
```
results in `Error: identifier expected, but found '1'`.
Here, `x` argument in `bar` has been renamed by [template arguments hygiene](https://nim-lang.org/docs/manual.html#templates-hygiene-in-templates) when template `foo` is instantiated. This can be seen in the `expandMacros` result displayed when compiling, after changing `x` in `bar` for `a`.

3. Obscure error message
```nim
template foo(a: int, b: int) =
  template bar(a: int): int =
    a == b

foo(1, 2)
```
results in `Error: identifier expected, but found '1'`.
&quot;&quot;&quot;</span>
nbSave()
</code></pre>
</section><script>
function toggleSourceDisplay() {
  var btn = document.getElementById("show")
  var source = document.getElementById("source");
  if (btn.innerHTML=="Show Source") {
    btn.innerHTML = "Hide Source";
    source.style.display = "block";
  } else {
    btn.innerHTML = "Show Source";
    source.style.display = "none";
  }
}
</script></body>
</html>